allprojects {
    group = 'com.talania'
    version = '0.1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java-library'

    tasks.named('jar') {
        def baseName = project.name
        if (project.name == 'core') {
            baseName = 'TalaniaCore'
        } else if (project.name == 'races') {
            baseName = 'TalaniaRaces'
        } else if (project.name == 'boss-fights') {
            baseName = 'TalaniaBossFights'
        }
        archiveBaseName.set(baseName)
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(25)
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

    // Configure javadoc to not fail on errors/warnings
    javadoc {
        options.addStringOption('Xdoclint:none', '-quiet')
        failOnError = false
    }

    dependencies {
        // Hytale server (local-only, provided by .local/server/HytaleServer.jar)
        compileOnly files(rootProject.file('.local/server/HytaleServer.jar'))

        // Testing
        testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    }
}
